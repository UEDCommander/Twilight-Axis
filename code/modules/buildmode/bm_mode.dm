/datum/buildmode_mode
	var/key = "oops"
	var/button_icon = 'icons/misc/buildmode.dmi' // TA EDIT
	var/button_icon_state // TA EDIT

	var/datum/buildmode/BM

	// would corner selection work better as a component?
	var/use_corner_selection = FALSE
	var/list/preview
	var/turf/cornerA
	var/turf/cornerB

/datum/buildmode_mode/New(datum/buildmode/BM)
	src.BM = BM
	preview = list()
	return ..()

/datum/buildmode_mode/Destroy()
	cornerA = null
	cornerB = null
	QDEL_LIST(preview)
	preview = null
	return ..()

/datum/buildmode_mode/proc/enter_mode(datum/buildmode/BM)
	return

/datum/buildmode_mode/proc/exit_mode(datum/buildmode/BM)
	return

/datum/buildmode_mode/proc/get_button_icon() // TA EDIT
	return button_icon || 'icons/misc/buildmode.dmi' // TA EDIT

/datum/buildmode_mode/proc/get_button_iconstate()
	return button_icon_state || "buildmode_[key]" // TA EDIT

/datum/buildmode_mode/proc/show_help(client/c)
	CRASH("No help defined, yell at a coder")

/datum/buildmode_mode/proc/change_settings(client/c)
	to_chat(c, span_warning("There is no configuration available for this mode"))
	return

/datum/buildmode_mode/proc/Reset()
	deselect_region()

/datum/buildmode_mode/proc/select_tile(turf/T, corner_to_select)
	var/overlaystate
	BM.holder.images -= preview
	switch(corner_to_select)
		if(AREASELECT_CORNERA)
			overlaystate = "greenOverlay"
		if(AREASELECT_CORNERB)
			overlaystate = "blueOverlay"

	var/image/I = image('icons/turf/overlays.dmi', T, overlaystate)
	I.plane = ABOVE_LIGHTING_PLANE
	preview += I
	BM.holder.images += preview
	return T

/datum/buildmode_mode/proc/highlight_region(region)
	BM.holder.images -= preview
	for(var/t in region)
		var/image/I = image('icons/turf/overlays.dmi', t, "redOverlay")
		I.plane = ABOVE_LIGHTING_PLANE
		preview += I
	BM.holder.images += preview

/datum/buildmode_mode/proc/deselect_region()
	BM.holder.images -= preview
	preview.Cut()
	cornerA = null
	cornerB = null

/datum/buildmode_mode/proc/handle_click(client/c, params, object)
	var/list/pa = params2list(params)
	var/left_click = pa.Find("left")
	if(use_corner_selection)
		if(left_click)
			if(!cornerA)
				cornerA = select_tile(get_turf(object), AREASELECT_CORNERA)
				if(cornerA) // TA EDIT
					BM.log_action("selected region corner A at [AREACOORD(cornerA)] in [key] mode.") // TA EDIT
				return
			if(cornerA && !cornerB)
				cornerB = select_tile(get_turf(object), AREASELECT_CORNERB)
				if(cornerB) // TA EDIT
					BM.log_action("selected region corner B at [AREACOORD(cornerB)] in [key] mode.") // TA EDIT
				to_chat(c, span_boldwarning("Region selected, if you're happy with your selection left click again, otherwise right click."))
				return
			handle_selected_area(c, params)
			deselect_region()
		else
			if(cornerA) // TA EDIT START
				BM.log_action("cancelled region selection in [key] mode starting at [AREACOORD(cornerA)].")
			else
				BM.log_action("cancelled region selection in [key] mode.") // TA EDIT END
			to_chat(c, span_notice("Region selection canceled!"))
			deselect_region()
	return

/datum/buildmode_mode/proc/handle_selected_area(client/c, params)
